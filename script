#!/usr/bin/env fish

if test -z "$MACHINE"
	read -f -P "what is the hostname of the machine to set up?\n> " machine
else
	set -f machine $MACHINE
end

if test -z "$DOTFILES_DIR"
	read -f -P "where is the config repository?\n> " config_location
else
	set -f config_location $DOTFILES_DIR
end

if test -z "$EDITOR"
	# echo "please make sure \$EDITOR is set"
	# exit 1
	echo "it seems \$EDITOR is unset, defaulting to `vi`"
	set -f EDITOR vi
end

switch $argv[1]
	case edit
		if not test (count $argv[2..]) -eq 0
			switch $argv[2]
				case nix nixos
					$EDITOR $config_location/nixos/.

				case darwin macos
					$EDITOR $config_location/darwin/.

				case home home-manager
					$EDITOR $config_location/home/.

				case flake
					$EDITOR $config_location/flake.nix

				case self setup script
					$EDITOR $config_location/script

				case wip-nixos
					$EDITOR $config_location/nixos/common/programs/work-in-progress.nix

				case wip-darwin
					$EDITOR $config_location/darwin/common/programs/work-in-progress.nix

				case '*'
					$EDITOR $config_location/$argv[2..]
			end
		else
			$EDITOR $config_location/.
		end

	case switch
		if not test (count $argv[2..]) -eq 0
			switch $argv[2]
				case nix nixos
					sudo nixos-rebuild switch --flake $config_location/.#$machine

				case darwin macos
					darwin-rebuild switch --flake $config_location/.#$machine

				# case home home-manager
				# 	home-manager switch --flake $config_location#$machine
				#
				# case both
				# 	sudo nixos-rebuild switch --flake $config_location/.#$machine
				# 	home-manager switch --flake $config_location/.#$machine

				case '*'
					echo "Unrecognized input."
					exit 1
			end
		else
			# # nix-darwin entrypoint
			# darwin-rebuild switch --flake $config_location/.#$machine
			# # nixos entrypoint
			# sudo nixos-rebuild switch --flake $config_location/.#$machine
			# # home manager entrypoint
			# home-manager switch --flake $config_location/.#$machine
			echo "Unrecognized input."
			exit 1
		end

	case cd
		cd $config_location

	case fzf find
		set current_dir $(pwd)
		cd $config_location
		nvim (fzf)
		cd $current_dir

	case rg search
		rg -g !user-dots $config_location -e $argv[2..]

	case lg git
		lazygit -p $config_location

	case aj format
		alejandra $config_location --experimental-config $config_location/alejandra.toml --exclude $config_location/home/user-dots

	case '*'
		echo "Unrecognized input."
		exit 1
end
